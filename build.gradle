buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-docker-plugin:2.6.8'
		classpath 'org.ajoberstar:gradle-git:1.3.2'
	}
}

plugins {
	id 'play'
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import org.gradle.plugins.ide.eclipse.model.SourceFolder
import org.ajoberstar.grgit.Grgit

apply plugin: 'org.ajoberstar.grgit'

def repo = Grgit.open(project.file('.'))
def describe = repo.describe()
if(describe && describe.startsWith('v')) {
	version = describe.substring(1)
} else {
	version = 'latest'
}

apply plugin: 'com.bmuschko.docker-remote-api'

docker {
	def env = System.env
	
	if(env.containsKey('DOCKER_HOST')) {
		url = "$env.DOCKER_HOST"
		
		if(env.containsKey('DOCKER_TLS_VERIFY')) {
			url = url.replace('tcp', 'https')
		} else {
			url = url.replace('tcp', 'http')
		}
		
		if(env.containsKey('DOCKER_CERT_PATH')) {
			certPath = file "$env.DOCKER_CERT_PATH"
		}
	} else {
		url = "http://${dockerHost}:2375"
	}
}

repositories {
	jcenter()
	maven {
		name "typesafe-maven-release"
		url "https://repo.typesafe.com/typesafe/maven-releases"
	}
	ivy {
		name "typesafe-ivy-release"
		url "https://repo.typesafe.com/typesafe/ivy-releases"
		layout "ivy"
	}
	maven {
		name "idgis-public"
		url "http://nexus.idgis.eu/content/groups/public/"
		credentials {
			username nexusUser 
			password nexusPassword
		}
	}
	maven {
		name "idgis-restricted"
		url "http://nexus.idgis.eu/content/groups/restricted/"
		credentials {
			username nexusUser 
			password nexusPassword
		}
	}
}

def scalaVersion = '2.11'
def playVersion = '2.4.6'

model {
	components {
		play {
			platform play: playVersion, scala: scalaVersion, java: '1.8'
			injectedRoutesGenerator = true
			
			sources {
				twirlTemplates {
					defaultImports = TwirlImports.JAVA
				}
			}
		}
	}
	
	distributions {
		playBinary {
			tasks.withType(org.gradle.jvm.tasks.Jar) {
				manifest {
					attributes("Implementation-Title": project.name)
					if(project.version) {
						attributes("Implementation-Version": project.version)
					}
				}
			}
		}
	}
	
	tasks {
		copyTar(Copy) {
			dependsOn createPlayBinaryTarDist
			from tarTree("${project.buildDir}/distributions/playBinary.tar")
			into "${project.buildDir}/docker"
		}

		createDockerfile(Dockerfile) {
			dependsOn copyTar
			destFile = project.file('build/docker/Dockerfile')
			from 'java'
			copyFile 'playBinary', '/opt'
			runCommand 'chmod u+x /opt/bin/playBinary'
			exposePort 9000
			defaultCommand "/opt/bin/playBinary"
		}

		buildImage(DockerBuildImage) {
			dependsOn createDockerfile
			inputDir = project.file('build/docker')
			tag = "idgis/geopublisher_viewer:${project.version}"
		}
	}
}

dependencies {
	play "com.typesafe.play:play-java-ws_${scalaVersion}:${playVersion}"

	play('nl.idgis.geoide:geoide-ogc-client:0.1.8') {
		exclude group: 'java3d', module: 'vecmath'
	}
	
	play "org.webjars:webjars-play_${scalaVersion}:2.4.0-1"
	play 'org.webjars:openlayers:3.5.0'
	play 'org.webjars:dojo:1.10.4'
	play 'org.webjars:bootstrap:3.3.5'
	play 'org.webjars:jquery:2.1.4'
	play 'java3d:vecmath:1.5.2'
	play 'org.jsoup:jsoup:1.8.3'
	play 'org.pegdown:pegdown:1.5.0'
	
	play('org.apache.zookeeper:zookeeper:3.4.5') {
		exclude group: 'javax.jms', module: 'jms'
		exclude group: 'com.sun.jdmk', module: 'jmxtools'
		exclude group: 'com.sun.jmx', module: 'jmxri'
		exclude group: 'org.jboss.netty', module: 'netty'
	}
}